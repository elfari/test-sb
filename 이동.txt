CREATE OR REPLACE FUNCTION FN_SIMILARITY_PCT (
    p_exist IN VARCHAR2,
    p_input IN VARCHAR2
) RETURN NUMBER IS

    /* 변수 선언부 */
    v_exist_clean   VARCHAR2(4000);
    v_input_clean   VARCHAR2(4000);
    v_exist_final   VARCHAR2(4000);
    v_input_final   VARCHAR2(4000);
    v_similarity    NUMBER := 0;

BEGIN
    /* 기존값 또는 입력값이 NULL이면 0 처리 */
    IF p_exist IS NULL OR p_input IS NULL THEN
        v_similarity := 0;
    ELSE
        /* 기존값 정제: 영문, 숫자, 한글, 중문을 제외한 모든 문자를 공백으로 치환 */
        /* 대소문자 무시를 위해 UPPER 적용 */
        v_exist_clean := REGEXP_REPLACE(UPPER(p_exist), '[^A-Z0-9가-힣' || UNISTR('\4E00-\9FFF') || ']', ' ');

        /* 입력값 정제: 동일한 로직 적용 */
        v_input_clean := REGEXP_REPLACE(UPPER(p_input), '[^A-Z0-9가-힣' || UNISTR('\4E00-\9FFF') || ']', ' ');

        /* 기존값 토큰화: 공백 기준으로 문자를 분리 */
        /* 중복 토큰 제거, 토큰 정렬 후 다시 병합 */
        SELECT LISTAGG(token, '') WITHIN GROUP (ORDER BY token)
          INTO v_exist_final
          FROM (
              SELECT DISTINCT REGEXP_SUBSTR(v_exist_clean, '[^ ]+', 1, LEVEL) AS token
                FROM DUAL
              CONNECT BY REGEXP_SUBSTR(v_exist_clean, '[^ ]+', 1, LEVEL) IS NOT NULL
          );

        /* 입력값 토큰화: 동일한 로직 적용 */
        SELECT LISTAGG(token, '') WITHIN GROUP (ORDER BY token)
          INTO v_input_final
          FROM (
              SELECT DISTINCT REGEXP_SUBSTR(v_input_clean, '[^ ]+', 1, LEVEL) AS token
                FROM DUAL
              CONNECT BY REGEXP_SUBSTR(v_input_clean, '[^ ]+', 1, LEVEL) IS NOT NULL
          );

        /* 유사도 계산 로직 */
        /* 정렬된 토큰 문자열이 일치하는 경우 */
        IF v_exist_final = v_input_final THEN
            /* 정렬까지 동일하고 원본 값도 완전히 동일한 경우 100 */
            IF p_exist = p_input THEN
                v_similarity := 100;
            /* 정렬된 토큰은 같으나 원본이 다른 경우 99 */
            ELSE
                v_similarity := 99;
            END IF;
        ELSE
            /* 그 외에는 오라클 내장 함수를 사용하여 문자열 유사도 계산 */
            v_similarity := UTL_MATCH.JARO_WINKLER_SIMILARITY(v_exist_final, v_input_final);
        END IF;

    END IF;

    /* 최종 결과 리턴 */
    RETURN v_similarity;

EXCEPTION
    WHEN OTHERS THEN
        RETURN 0;
END FN_SIMILARITY_PCT;
/


