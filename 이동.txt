CREATE OR REPLACE FUNCTION FNSIMPCT (
    p_exist IN VARCHAR2,
    p_input IN VARCHAR2
) RETURN NUMBER
IS
    v_exist_clean   VARCHAR2(4000);
    v_input_clean   VARCHAR2(4000);
    v_exist_sorted  VARCHAR2(4000);
    v_input_sorted  VARCHAR2(4000);
    v_similarity    NUMBER := 0;
BEGIN
    IF p_exist IS NULL OR p_input IS NULL THEN
        RETURN 0;
    END IF;

    IF UPPER(p_exist) = UPPER(p_input) THEN
        RETURN 100;
    END IF;

    v_exist_clean := REGEXP_REPLACE(UPPER(p_exist), '[^A-Z0-9가-힣一-龥]', ' ');
    v_input_clean := REGEXP_REPLACE(UPPER(p_input), '[^A-Z0-9가-힣一-龥]', ' ');

    WITH exist_tokens AS (
        SELECT DISTINCT REGEXP_SUBSTR(v_exist_clean, '[^ ]+', 1, LEVEL) AS token
        FROM dual
        CONNECT BY REGEXP_SUBSTR(v_exist_clean, '[^ ]+', 1, LEVEL) IS NOT NULL
    )
    SELECT LISTAGG(token, ' ') WITHIN GROUP (ORDER BY token)
    INTO v_exist_sorted
    FROM exist_tokens;

    WITH input_tokens AS (
        SELECT DISTINCT REGEXP_SUBSTR(v_input_clean, '[^ ]+', 1, LEVEL) AS token
        FROM dual
        CONNECT BY REGEXP_SUBSTR(v_input_clean, '[^ ]+', 1, LEVEL) IS NOT NULL
    )
    SELECT LISTAGG(token, ' ') WITHIN GROUP (ORDER BY token)
    INTO v_input_sorted
    FROM input_tokens;

    IF v_exist_sorted = v_input_sorted THEN
        IF UPPER(p_exist) <> UPPER(p_input) THEN
            v_similarity := 99;
        ELSE
            v_similarity := 100;
        END IF;
    ELSE
        DECLARE
            v_exist_str VARCHAR2(4000);
            v_input_str VARCHAR2(4000);
            v_common_len NUMBER := 0;
            v_exist_len NUMBER;
            v_input_len NUMBER;
        BEGIN
            v_exist_str := REPLACE(v_exist_sorted, ' ', '');
            v_input_str := REPLACE(v_input_sorted, ' ', '');

            -- 원본 문자열 길이 그대로 사용
            v_exist_len := LENGTH(p_exist);
            v_input_len := LENGTH(p_input);

            FOR i IN 1 .. LENGTH(v_exist_str) LOOP
                IF INSTR(v_input_str, SUBSTR(v_exist_str, i, 1)) > 0 THEN
                    v_common_len := v_common_len + 1;
                END IF;
            END LOOP;

            v_similarity := ROUND((v_common_len * 2) / (v_exist_len + v_input_len) * 100);
        END;
    END IF;

    RETURN v_similarity;
END;
/